plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven {
        url = uri("https://mvn.devos.one/releases")
    }
	exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"seasonallods" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	implementation("xyz.bluspring.modernnetworking:modernnetworking-api:1.2.4")
	modImplementation("xyz.bluspring.modernnetworking:modernnetworking-fabric:1.2.4+1.21.10")
	modImplementation("net.fabricmc:fabric-language-kotlin:1.13.7+kotlin.2.2.21")
	modCompileOnly("maven.modrinth:flashback:AwyE5yQ2")
	modRuntimeOnly("maven.modrinth:flashback:AwyE5yQ2")
	modCompileOnly("maven.modrinth:voxy:0.2.6-alpha")
	modRuntimeOnly("maven.modrinth:voxy:0.2.6-alpha")
	modRuntimeOnly("maven.modrinth:sodium:mc1.21.10-0.7.2-fabric")
    // modCompileOnly("maven.modrinth:sodium:mc1.21.10-0.7.2-fabric")
	runtimeOnly("org.lwjgl:lwjgl")
    runtimeOnly("org.lwjgl:lwjgl-lmdb")
    runtimeOnly("org.lwjgl:lwjgl-zstd")
    runtimeOnly("org.lwjgl:lwjgl:3.3.3:natives-windows")
    runtimeOnly("org.lwjgl:lwjgl:3.3.3:natives-linux")
    runtimeOnly("org.lwjgl:lwjgl-lmdb:3.3.3:natives-windows")
    runtimeOnly("org.lwjgl:lwjgl-zstd:3.3.3:natives-windows")
    runtimeOnly("org.lwjgl:lwjgl-lmdb:3.3.3:natives-linux")
    runtimeOnly("org.lwjgl:lwjgl-zstd:3.3.3:natives-linux")
	runtimeOnly('org.rocksdb:rocksdbjni:10.2.1')
	runtimeOnly('org.apache.commons:commons-pool2:2.12.0')
	runtimeOnly('org.lz4:lz4-java:1.8.0')
	runtimeOnly('org.tukaani:xz:1.10')

	runtimeOnly("com.moulberry:mixinconstraints:1.0.8")

    // Lattice (config)
    modRuntimeOnly("com.moulberry:lattice:1.2.18") {
        attributes {
            attribute(Attribute.of("earth.terrarium.cloche.modLoader", String), "fabric")
        }
    }
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}